// Parameters
real error = 0.1;
int n=6;
// Mesh
border ba(t=-2, 2){x=t; y=-2; label=1;}
border bb(t=-2, 2){x=2; y=t; label=2;}
border bc(t=-2, 2){x=-t; y=2; label=3;}
border bd(t=-2, 2){x=-2; y=-t; label=4;}
mesh Th = buildmesh(ba(n) + bb(n) + bc(n) + bd(n));

// Fespace
fespace Vh(Th, P1);
Vh u, v;

// Function
func f = cos(pi*x);

// Problem
problem Poisson(u, v, solver=CG, eps=1.e-6)
    = int2d(Th)(
          dx(u)*dx(v)
        + dy(u)*dy(v)
    )
    - int2d(Th)(
          f*v
    );

  plot(Th, wait=1);
// Adaptmesh loop
for (int i = 0; i < 4; i++){
    Poisson;
    Th = adaptmesh(Th, u, err=error, iso=true);
    error = error/2;
    plot(Th, wait=1);
}

// Plot
plot(u, dim=3, fill=true);
